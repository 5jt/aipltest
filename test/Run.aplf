 Run←{
    ⍝ Run tests in ##.tests namespace
    ⍝ ⍵: function names to test (empty=all)
    ⍝ Prints summary and writes detailed logs to logs/{fn}.log
    ⍝ Returns counts: pass fail broke

    ⍝ Constants
     ROOTDIR←'/Users/sjt/Projects/aipltest/'
     LOGDIR←ROOTDIR,'logs/'
     TESTNS←##.tests
     FUNNS←##.fun

    ⍝ Identify functions to test
     fns←(⊃∩/FUNNS TESTNS{⍺.⎕NL ⍵}¨-3 2){
         0=≢⍵:⍺
         _←{0=≢⍵:'' ⋄ ''⊣⎕←'Not found: ',⍕⍵}⍵~⍺
         ⍺∩⍵
     }⍵

     0=≢fns:0 0 0⊣⎕←'No tests found'

     execTest←{
         (fn tst)←⍺ ⍵
         expn←'tst.larg '/⍨×|tst.⎕NC'larg' ⍝ dyadic?
         expn,←'FUNNS.',fn,' tst.rarg'
         ⎕←'[',fn,'] ',tst.description
         tst.(result dm)←tst{0::(¯1 ⋄ ⎕DM) ⋄ (⍎⍵ ⋄ '')}expn
         tst.rc←{×≢⍵.dm:¯1 ⋄ ⍵.result≡⍵.expected}tst ⍝ return code
         tst
     }

     et←(⊂¨fns)execTest¨¨TESTNS ⎕VGET fns ⍝ executed tests

     reportTest←{
         eg←(
             '    Expected: ',⍕⍵.expected
             '    Got: ',⍕⍵.result
         )
         bfp←¯1 0 1⍳⍵.rc
         _←bfp⊃'BROKE' 'FAIL' 'PASS'
         r←⊂_,' - ',⍵.description
         r,←bfp⊃(⊂⍵.dm ⋄ eg ⋄ '')
         ∊r,¨⊂⎕UCS 13 10
     }

     logs←⊃∘(,/)¨reportTest¨¨et
     _←logs ⎕NPUT¨LOGDIR∘{(⍺,⍵,'.log' ⋄ 1)}¨fns

     pfb←+⌿table←↑{+/1 0 ¯1∘.=⍵}¨et.rc
     ⎕←'' 'pass' 'fail' 'broke'⍪(fns,table)⍪(⊂'TOTAL'),pfb

     pfb ⍝ pass fail broke
 }
