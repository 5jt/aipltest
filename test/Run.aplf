 Run←{
    ⍝ Run tests in ##.tests for functions in ##.fun
    ⍝ ⍵: function names to test (empty=all)
    ⍝ Prints summary and writes detailed logs to logs/{fn}.log
    ⍝ Returns counts: pass fail broke

     ⍺←0
     verbose←⍺

    ⍝ Constants
     ROOTDIR←'/Users/sjt/Projects/aipltest/'
     LOGDIR←ROOTDIR,'logs/'
     TESTNS←##.tests
     FUNNS←##.fun

    ⍝ Identify functions with tests
     fns←(⊃∩/FUNNS TESTNS{⍺.⎕NL ⍵}¨-3 2){
         0=≢⍵:⍺
         _←{0=≢⍵:'' ⋄ ''⊣⎕←'Incomplete: ',⍕⍵}⍵~⍺
         ⍺∩⍵
     },¨⍵

     0=≢fns:0 0⊣⎕←'No tests found'

     et←(⊂¨fns)execTest¨¨TESTNS ⎕VGET fns ⍝ executed tests

    ⍝ report results to logfiles
     logs←⊃∘(,/)¨reportTest¨¨et
     _←logs ⎕NPUT¨LOGDIR∘{(⍺,⍵,'.log' ⋄ 1)}¨fns

    ⍝ summarise results
     table←↑{+/1 0∘.=⍵}¨et.pass

     _←verbose{⍺=0:'' ⋄ ''⊢⎕←⍵}'' 'pass' 'fail'⍪fns,table

     +⌿table ⍝ return pass fail counts
 }
