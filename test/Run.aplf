 Run←{
    ⍝ Run tests in ##.tests namespace
    ⍝ ⍵: function names to test (empty=all)
    ⍝ Prints summary and writes detailed logs to logs/{fn}.log
    ⍝ Returns counts: pass fail broke

     ⍺←0
     verbose←⍺
    ⍝ Constants
     ROOTDIR←'/Users/sjt/Projects/aipltest/'
     LOGDIR←ROOTDIR,'logs/'
     TESTNS←##.tests
     FUNNS←##.fun

    ⍝ Identify functions to test
     fns←(⊃∩/FUNNS TESTNS{⍺.⎕NL ⍵}¨-3 2){
         0=≢⍵:⍺
         _←{0=≢⍵:'' ⋄ ''⊣⎕←'Not found: ',⍕⍵}⍵~⍺
         ⍺∩⍵
     },¨⍵

     0=≢fns:0 0 0⊣⎕←'No tests found'

     execTest←{
         tst←(breaks:0)⎕VSET{(n←⍵.⎕NL 2 ⋄ ⍵ ⎕VGET n)}⍵
         expn←'tst.larg '/⍨×|tst.⎕NC'larg' ⍝ dyadic?
         expn,←'FUNNS.',⍺,' tst.rarg'
         _←verbose{⍺:⎕←⍵ ⋄ ⍬}'[',⍺,'] ',⍵.description
         tst.(result dm en)←tst{0::(¯1 ⋄ ⎕DM ⋄ ⎕EN) ⋄ (⍎⍵ ⋄ '' ⋄ 0)}expn
         tst.pass←{
             ×⍵.breaks:=/⍵.(breaks en) ⍝ broke as expected?
             ×⍵.en:0 ⍝ broke unexpectedly
             ⍵.result≡⍵.expected
         }tst ⍝ return code
         tst
     }

     et←(⊂¨fns)execTest¨¨TESTNS ⎕VGET fns ⍝ executed tests

     reportTest←{
         exp←'    Expected: ',⍕{×⍵.breaks:⎕EM ⍵.breaks ⋄ ⍵.expected}⍵
         got←'    Got: ',⍕(⍵.result ⋄ ⊃⍵.dm)⊃⍨1+×⍵.en
         r←⊂('FAIL' 'PASS'⊃⍨1+⍵.pass),' - ',⍵.description
         r,←⍵.pass{⍺:'' ⋄ ⍵}exp got
         ∊r,¨⊂⎕UCS 13 10
     }

     logs←⊃∘(,/)¨reportTest¨¨et
     _←logs ⎕NPUT¨LOGDIR∘{(⍺,⍵,'.log' ⋄ 1)}¨fns

     pf←+⌿table←↑{+/1 0∘.=⍵}¨et.pass
     ⎕←'' 'pass' 'fail'⍪(fns,table)⍪(⊂'TOTAL'),pf

     pf ⍝ pass fail counts
 }
