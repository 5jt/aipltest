 value←{
     ⍝ Extract values from Ddictionary, Object, Table, Xref or Array
     ⍝ Returns:
     ⍝   Dictionary: values vector (N elements, excluding default if present)
     ⍝   Object: variable values (alphabetically sorted by name)
     ⍝   Table: values matrix
     ⍝   Xref: values matrix
     ⍝   Array: the array itself

     isDict ⍵:{
         (vals keys)←⍵
         vals↑⍨≢keys              ⍝ 1st N elements (excludes N+1 default)
     }⍵

     isObj ⍵:⍵ ⎕VGET key ⍵        ⍝ Object: values in alpha order

     isTable ⍵:1⊃⍵                ⍝ Table: values matrix

     isXref ⍵:1⊃⍵                 ⍝ Xref: values matrix

     2=⎕NC'⍵':⍵⊣⎕SIGNAL 4/⍨0=≢⍴⍵  ⍝ non-scalar array

     'argument must be dict, obj, table, xref or array'⎕SIGNAL 11
 }
