 at←{
    ⍝ Functional indexing for dictionaries and objects
    ⍝ with explicit or implicit outrange values
    ⍝ later: tables, xrefs and arrays
    ⍝ Syntax: sel ← ⍺ at keys
    ⍝ Returns values for keys
    ⍝ A dictionary can have an explicit default outrange value
    ⍝ Otherwise, outrange keys signal index errors
     ind←⍺{
         isDict ⍺:(key ⍺){
             nestedKey←1≠≡⍺
             singleIndex←nestedKey{0=≢⍴⍵:1 ⋄ ⍺:1=≡⍵ ⋄ 0=≡⍵}⍵
             w←⊂⍣(nestedKey∧singleIndex∧1=≡⍵)⊢⍵
             ⊃⍣singleIndex⊢⍺⍳w
         }⍵
         isObj ⍺:(key ⍺){
             ~isStrung ⍵:'indices not string/s'⎕SIGNAL 11
             ¯1∊⎕NC,⍵:'indices not valid names'⎕SIGNAL 11
             singleIndex←0 1∊⍨≡⍵
             w←(⊂∘,)⍣singleIndex⊢⍵
             ⊃⍣singleIndex⊢⍺⍳w
         }⍵
         isTable ⍺:⍺{
             (ir ic)←⍺{
                 (mat nms)←⍺ ⍝ value matrix; col names
                 11 83∨.=⎕DR ⍵:(⍵ ⋄ ⍳≢nms)             ⍝ row/s
                 isStrung ⍵:(⍳≢mat ⋄ nms⍳⊆⍵)               ⍝ col name/s
                 2≠≢⍵:⎕SIGNAL 11
                 (ri ci)←⍵
                 (83=⎕DR ri)⍲isStrung ci:⎕SIGNAL 11
                 (ri ⋄ nms⍳⊆ci)                        ⍝ rows & cols
             }⍵
             (⍴value ⍺){
                 ⎕IO←0
                 c←⊃∘.,/⍵
                 1∊∊⍺∘<¨c:⎕SIGNAL 3
                 s←+/≢∘⍴¨⍵
                 ⊃⍣(s=0)⊢,⍣(s=1)⊢1+⍺∘⊥¨c-1
             }ir ic
         }⍵
         'not dictionary, object or table'⎕SIGNAL 11
     }⍵
    ⊃⍣(0=≢⍴ind)⊢(,value ⍺)[ind]
 }
