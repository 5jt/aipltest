 at←{
    ⍝ Functional indexing for dictionaries and objects
    ⍝ with explicit or implicit outrange values
    ⍝ later: tables, xrefs and arrays
    ⍝ Syntax: sel ← ⍺ at keys
    ⍝ Returns values for keys
    ⍝ A dictionary can have an explicit default outrange value
    ⍝ Otherwise, outrange keys signal index errors
     ind←⍺{
         isDict ⍺:(key ⍺){
             nestedKey←1≠≡⍺
             singleIndex←nestedKey{0=≢⍴⍵:1 ⋄ ⍺:1=≡⍵ ⋄ 0=≡⍵}⍵
             w←⊂⍣(nestedKey∧singleIndex∧1=≡⍵)⊢⍵
             ⊃⍣singleIndex⊢⍺⍳w
         }⍵
         isObj ⍺:(key ⍺){
             ~isStrung ⍵:'indices not string/s'⎕SIGNAL 11
             ¯1∊⎕NC,⍵:'indices not valid names'⎕SIGNAL 11
             singleIndex←0 1∊⍨≡⍵
             w←(⊂∘,)⍣singleIndex⊢⍵
             ⊃⍣singleIndex⊢⍺⍳w
         }⍵
         atm←{
             ⎕IO←0
             sv←⍴value ⍺
             c←⊃∘.,/⍵
             1∊∊sv∘<¨c:⎕SIGNAL 3
             sr←+/≢∘⍴¨⍵
             ⊃⍣(sr=0)⊢,⍣(sr=1)⊢1+sv∘⊥¨c-1
         }
         isTable ⍺:⍺{
             (ir ic)←⍺{
                 (mat cols)←⍺ ⍝ value matrix; col names
                 11 83∨.=⎕DR ⍵:(⍵ ⋄ ⍳≢cols)             ⍝ row/s
                 isStrung ⍵:(⍳≢mat ⋄ cols⍳⊆⍵)               ⍝ col name/s
                 2≠≢⍵:⎕SIGNAL 11
                 (ri ci)←⍵
                 (83=⎕DR ri)⍲isStrung ci:⎕SIGNAL 11
                 (ri ⋄ cols⍳⊆ci)                        ⍝ rows & cols
             }⍵
             ⍺ atm ir ic
         }⍵
         isXref ⍺:⍺{
             (ir ic)←⍺{
                 (mat cols rows)←⍺
                 2≠≢⍵:⎕SIGNAL 11
                 1=≡⍵:⎕SIGNAL 11
                 1 1≢isStrung¨⍵:⎕SIGNAL 11
                 1∨.≠×≢¨⍵:⎕SIGNAL 3
                 (ir ic)←rows cols⍳∘⊆∘,¨⍵
                 ∨/∊ir ic>≢¨rows cols:⎕SIGNAL 3
                 ir ic
             }⍵
             ⍺ atm ir ic
         }⍵
         'not dictionary, object or table'⎕SIGNAL 11
     }⍵
     ⊃⍣(0=≢⍴ind)⊢(∊⍴∘⊆¨⍵)⍴(,value ⍺)[ind]
 }
