 at←{
    ⍝ Functional indexing for dictionaries, objects, tables and xrefs
    ⍝ with explicit or implicit outrange values
    ⍝ later: arrays
    ⍝ Syntax: values ← ⍺ at keys
    ⍝
    ⍝ A dictionary can have an explicit default outrange value
    ⍝ Otherwise, outrange keys signal index errors
     (ind shp)←⍺{
         isDict ⍺:(key ⍺){
             nestedKey←1≠≡⍺
             singleIndex←(1 nestedKey)∨.∧0 1=≡⍵
             w←⊂∘,⍣(nestedKey∧singleIndex)⊢⍵
             s←singleIndex{⍺:⍬ ⋄ ⍴⍵}⍵
             (⍺⍳w ⋄ s)
         }⍵
         isObj ⍺:(key ⍺){
             ~isStrung ⍵:'indices not string/s'⎕SIGNAL 11
             ¯1∊⎕NC,⍵:'indices not valid names'⎕SIGNAL 11
             singleIndex←0 1∊⍨≡⍵
             w←(⊂∘,)⍣singleIndex⊢⍵
             s←singleIndex{⍺:⍬ ⋄ ⍴⍵}⍵
             (⍺⍳w ⋄ s)
         }⍵
         atm←{
             ⎕IO←0
             sv←⍴value ⍺
             c←⊃∘.,/⍵
             1∊∊sv∘<¨c:⎕SIGNAL 3
             sr←+/≢∘⍴¨⍵
             ⊃⍣(sr=0)⊢,⍣(sr=1)⊢1+sv∘⊥¨c-1
         }
         isTable ⍺:⍺{
             (ir ic)←⍺{
                 (mat cols)←⍺ ⍝ value matrix; col names
                 11 83∨.=⎕DR ⍵:(⍵ ⋄ ⍳≢cols)             ⍝ row/s
                 isStrung ⍵:(⍳≢mat ⋄ cols⍳⊆⍵)           ⍝ col name/s
                 2≠≢⍵:⎕SIGNAL 11
                 (ri ci)←⍵
                 (83=⎕DR ri)⍲isStrung ci:⎕SIGNAL 11
                 (ri ⋄ cols⍳⊆ci)                        ⍝ rows & cols
             }⍵
             i←⍺ atm ir ic
             (i ⋄ ⍴i)
         }⍵
         isXref ⍺:⍺{
             (ir ic)←⍺{
                 (mat cols rows)←⍺
                 2≠≢⍵:⎕SIGNAL 11
                 1=≡⍵:⎕SIGNAL 11
                 1 1≢isStrung¨⍵:⎕SIGNAL 11
                 1∨.≠×≢¨⍵:⎕SIGNAL 3
                ⍝ row/col labels are all strings
                ⍝ so treat string or char index x as ⊂,x
                 (ir ic)←rows cols{s←(≡⍵)∊0 1 ⋄ ⍺⍳⊂∘,⍣s⊢⍵}¨⍵
                 ∨/∊ir ic>≢¨rows cols:⎕SIGNAL 3
                 ir ic
             }⍵
             i←⍺ atm ir ic
             (i ⋄ ⍴i)
         }⍵
         'not dictionary, object, table or xref'⎕SIGNAL 11
     }⍵
     ⊃⍣(0=≢⍴ind)⊢shp⍴(,value ⍺)[ind]
 }
